plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
	id 'idea'
	id 'eclipse'
    id "edu.wpi.first.GradleRIO" version "2019.4.1"
}

group = 'com.github.frc5431'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // compile wpilib()
    // compile ctre()
    // compile navx()
    compile wpi.deps.wpilib()
    compile wpi.deps.vendor.java()
    nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
    testCompile 'junit:junit:4.12'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    // testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

//To install this as a dependency
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'GNU GENERAL PUBLIC LICENSE, Version 4.0'
                    url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    distribution 'repo'
                }
            }
        }
    }
}

task buildJar(type: Jar) {
    /*manifest {
        attributes 'Main-Class': 'frc.team5431.titan.examples.TitanUtilRobot'
    }*/
    baseName = 'titan-util'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    doLast {
        copy {
            from 'build/libs/titan-util.jar'
            into '.'
        }
    }
}


javadoc {
    title "TitanUtil API"

    doLast {
        println "Coping to docs folder"
        copy {
            from "build/docs/javadoc"
            into "docs"
            include "**"
        }
        println "Finished Copying"
    }
}