plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
	id 'idea'
	id 'eclipse'
    id "edu.wpi.first.GradleRIO" version "2020.2.2"
}

group = 'com.github.frc5431'

// sourceCompatibility = JavaVersion.VERSION_11
// targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

List<String> wpilibDesktopJni(String platform) {
    return ["edu.wpi.first.thirdparty.frc2020.opencv:opencv-jni:${wpi.opencvVersion}:${platform}@jar".toString(),
            "edu.wpi.first.hal:hal-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.wpiutil:wpiutil-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.ntcore:ntcore-jni:${wpi.wpilibVersion}:${platform}@jar".toString(),
            "edu.wpi.first.cscore:cscore-jni:${wpi.wpilibVersion}:${platform}@jar".toString()]
}

dependencies {
    // compile wpilib()
    // compile ctre()
    // compile navx()
    compileOnly wpi.deps.wpilib()
    compileOnly wpi.deps.vendor.java()

    testImplementation wpi.deps.wpilib()
    testImplementation wpi.deps.vendor.java()
    
    testImplementation(wpilibDesktopJni(wpi.platforms.desktop))

    testImplementation 'junit:junit:4.12'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.6'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

//To install this as a dependency
install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'GNU GENERAL PUBLIC LICENSE, Version 4.0'
                    url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
                    distribution 'repo'
                }
            }
        }
    }
}

task buildJar(type: Jar) {
    /*manifest {
        attributes 'Main-Class': 'frc.team5431.titan.examples.TitanUtilRobot'
    }*/
    baseName = 'titan-util'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar

    doLast {
        copy {
            from 'build/libs/titan-util.jar'
            into '.'
        }
    }
}

javadoc {
    title "TitanUtil API"

    doLast {
        println "Coping to docs folder"
        copy {
            from "build/docs/javadoc"
            into "docs"
            include "**"
        }
        println "Finished Copying"
    }
}